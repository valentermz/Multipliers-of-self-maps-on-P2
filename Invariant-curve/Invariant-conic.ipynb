{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Examples of quadratic self-maps of $\\mathbb{P}^2$ with an invariant conic\n",
    "\n",
    "We define a 1-parameter family of quadratic self-maps $f_\\alpha \\colon \\mathbb{P}^2 \\to \\mathbb{P}^2$ having an invariant conic and study some properties of the associated foliations and the multipliers of the fixed-points on the invariant conic."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "%display latex"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "var('x0 x1 x2')\n",
    "coeff = var('p0 p1 p2 p3 p4 p5 q0 q1 q2 q3 q4 q5 r0 r1 r2 r3 r4 r5')"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "# All conics being projectively equivalent, we can assume that our conic is given by:\n",
    "g = x1*x2 - x0^2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [],
   "source": [
    "# We consider self maps f given by f = [P:Q:R]\n",
    "\n",
    "P = p0*x0^2 + p1*x0*x1 + p2*x0*x2 + p3*x1^2 + p4*x1*x2 + p5*x2^2\n",
    "Q = q0*x0^2 + q1*x0*x1 + q2*x0*x2 + q3*x1^2 + q4*x1*x2 + q5*x2^2\n",
    "R = r0*x0^2 + r1*x0*x1 + r2*x0*x2 + r3*x1^2 + r4*x1*x2 + r5*x2^2"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "**Remark:** In general, requiring $g$ to be an invariant curve for $f$ imposes 9 quadratic conditions on the coefficients of $P,Q,R$. These equations are constructed below."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Degree 0: -p5^2 + q5*r5\n",
      "\n",
      "Degree 1: -2*p2*p5 + q5*r2 + q2*r5\n",
      "\n",
      "Degree 2: -p2^2 - 2*p0*p5 - 2*p4*p5 + q5*r0 + q2*r2 + q5*r4 + q0*r5 + q4*r5\n",
      "\n",
      "Degree 3: -2*p0*p2 - 2*p2*p4 - 2*p1*p5 + q2*r0 + q5*r1 + q0*r2 + q4*r2 + q2*r4 + q1*r5\n",
      "\n",
      "Degree 4: -p0^2 - 2*p1*p2 - 2*p0*p4 - p4^2 - 2*p3*p5 + q0*r0 + q4*r0 + q2*r1 + q1*r2 + q5*r3 + q0*r4 + q4*r4 + q3*r5\n",
      "\n",
      "Degree 5: -2*p0*p1 - 2*p2*p3 - 2*p1*p4 + q1*r0 + q0*r1 + q4*r1 + q3*r2 + q2*r3 + q1*r4\n",
      "\n",
      "Degree 6: -p1^2 - 2*p0*p3 - 2*p3*p4 + q3*r0 + q1*r1 + q0*r3 + q4*r3 + q3*r4\n",
      "\n",
      "Degree 7: -2*p1*p3 + q3*r1 + q1*r3\n",
      "\n",
      "Degree 8: -p3^2 + q3*r3\n",
      "\n"
     ]
    }
   ],
   "source": [
    "var('t')\n",
    "\n",
    "composition = g.subs([x0==P, x1==Q, x2==R])  # g compose f\n",
    "composition = composition.subs([x0==t, x1==t^2, x2==1])  # rational parametrization of g\n",
    "\n",
    "TaylorCFS = []  # Taylor series expanssion wrt 't'\n",
    "\n",
    "current = composition.simplify_rational().numerator()\n",
    "\n",
    "for i in range(9):\n",
    "    coefficient = current.subs(t==0).factor()\n",
    "    print 'Degree {}: {}\\n'.format(i, coefficient)\n",
    "    TaylorCFS.append(coefficient)\n",
    "    current = diff(current, t) / (i + 1)\n",
    "    \n",
    "assert current == 0"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "**Optional:** The following code can verify that the ideal generated by the above quadratic equations indeed has dimension 9\n",
    "\n",
    "```python\n",
    "del (p0, p1, p2, p3, p4, p5, q0, q1, q2, q3, q4, q5, r0, r1, r2, r3, r4, r5)\n",
    "\n",
    "R.<p0, p1, p2, p3, p4, p5, q0, q1, q2, q3, q4, q5, r0, r1, r2, r3, r4, r5> = PolynomialRing(QQ, 18)\n",
    "\n",
    "I = ideal(-p5^2 + q5*r5,\n",
    "          -2*p2*p5 + q5*r2 + q2*r5,\n",
    "          -p2^2 - 2*p0*p5 - 2*p4*p5 + q5*r0 + q2*r2 + q5*r4 + q0*r5 + q4*r5,\n",
    "          -2*p0*p2 - 2*p2*p4 - 2*p1*p5 + q2*r0 + q5*r1 + q0*r2 + q4*r2 + q2*r4 + q1*r5,\n",
    "          -p0^2 - 2*p1*p2 - 2*p0*p4 - p4^2 - 2*p3*p5 + q0*r0 + q4*r0 + q2*r1 + q1*r2 + q5*r3 + q0*r4 + q4*r4 + q3*r5,\n",
    "          -2*p0*p1 - 2*p2*p3 - 2*p1*p4 + q1*r0 + q0*r1 + q4*r1 + q3*r2 + q2*r3 + q1*r4,\n",
    "          -p1^2 - 2*p0*p3 - 2*p3*p4 + q3*r0 + q1*r1 + q0*r3 + q4*r3 + q3*r4,\n",
    "          -2*p1*p3 + q3*r1 + q1*r3,\n",
    "          -p3^2 + q3*r3\n",
    "         )\n",
    "\n",
    "print 'Dimension(I) =', I.dimension()\n",
    "```\n",
    "\n",
    "`Dimension(I) = 9`"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Concrete examples: 1-parameter family\n",
    "\n",
    "Below we define a 1-parameter family of endomorphisms with the following properties:\n",
    "* $g$ is an invariant curve,\n",
    "* $\\{x_0=0\\}$ is an invariant line,\n",
    "* f permutes the point of intersections of the two curves above (and so they are not fixed points).\n",
    "\n",
    "In particular, the 3 fixed points on the conic belong to the affine chart $x_0=1$. Moreover, the position of such fixed points is constant along the family."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [],
   "source": [
    "var('alpha') # only free parameter in the family\n",
    "\n",
    "ex_1p = [p0==1, p1==3, p2==-4, p3==0, p4==0, p5==0,\n",
    "         q0==alpha, q1==0, q2==1, q3==0, q4==-alpha-1/2, q5==-1/2,\n",
    "         r0==0, r1==-48, r2==0, r3==-18, r4==-32, r5==0]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "<html><script type=\"math/tex; mode=display\">\\newcommand{\\Bold}[1]{\\mathbf{#1}}x_{0}^{2} + 3 \\, x_{0} x_{1} - 4 \\, x_{0} x_{2}</script></html>"
      ],
      "text/plain": [
       "x0^2 + 3*x0*x1 - 4*x0*x2"
      ]
     },
     "execution_count": 7,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "Pex = P.subs(ex_1p).expand(); Pex"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "<html><script type=\"math/tex; mode=display\">\\newcommand{\\Bold}[1]{\\mathbf{#1}}\\alpha x_{0}^{2} - \\alpha x_{1} x_{2} + x_{0} x_{2} - \\frac{1}{2} \\, x_{1} x_{2} - \\frac{1}{2} \\, x_{2}^{2}</script></html>"
      ],
      "text/plain": [
       "alpha*x0^2 - alpha*x1*x2 + x0*x2 - 1/2*x1*x2 - 1/2*x2^2"
      ]
     },
     "execution_count": 8,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "Qex = Q.subs(ex_1p).expand(); Qex"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "<html><script type=\"math/tex; mode=display\">\\newcommand{\\Bold}[1]{\\mathbf{#1}}-48 \\, x_{0} x_{1} - 18 \\, x_{1}^{2} - 32 \\, x_{1} x_{2}</script></html>"
      ],
      "text/plain": [
       "-48*x0*x1 - 18*x1^2 - 32*x1*x2"
      ]
     },
     "execution_count": 9,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "Rex = R.subs(ex_1p).expand(); Rex"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "TaylorCFS = [0, 0, 0, 0, 0, 0, 0, 0, 0]\n"
     ]
    }
   ],
   "source": [
    "# Verify that each element of the family satisfies the quadratic system 'TaylorCFS'\n",
    "print 'TaylorCFS =', [int(tcf.subs(ex_1p).expand()) for tcf in TaylorCFS]"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Associated foliation and lack of invariance of g\n",
    "\n",
    "Let $X=(X_1,X_2)$ be the associated vector field, and let $\\omega=X_2dx - X_1dy$ be the dual 2-form. Note that $dg = ydx + xdy$.\n",
    "\n",
    "The foliation $\\omega=0$ has g as an invariant curve if and only if $ \\omega =  h dg + g\\eta$, for some $h,\\eta$. In particular we must have that $\\omega\\wedge dg$ vanishes over $\\{g=0\\}$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "<html><script type=\"math/tex; mode=display\">\\newcommand{\\Bold}[1]{\\mathbf{#1}}\\left(x, y\\right)</script></html>"
      ],
      "text/plain": [
       "(x, y)"
      ]
     },
     "execution_count": 11,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "var('x y')  # represent x1/x0, x2/x0"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "metadata": {},
   "outputs": [],
   "source": [
    "# The associated vector field\n",
    "X1 = (x*Pex - Qex).subs([x0==1, x1==x, x2==y])\n",
    "X2 = (y*Pex - Rex).subs([x0==1, x1==x, x2==y])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "<html><script type=\"math/tex; mode=display\">\\newcommand{\\Bold}[1]{\\mathbf{#1}}{\\left(6 \\, x^{2} + 2 \\, x - 1\\right)}^{2} {\\left(x + 1\\right)}^{2}</script></html>"
      ],
      "text/plain": [
       "(6*x^2 + 2*x - 1)^2*(x + 1)^2"
      ]
     },
     "execution_count": 13,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "omega = (X2, -X1)\n",
    "dg = (y,x)\n",
    "\n",
    "wedge = x*X2 + y*X1\n",
    "\n",
    "wedge.subs(y==1/x).simplify_rational().numerator().factor()"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "**Note:** This is not identically zero, so the curve g is not invariant for any example in our family."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Lack of relative fixed-point formula\n",
    "\n",
    "The singular points on the curve g are the same for all elements in the family:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "metadata": {},
   "outputs": [],
   "source": [
    "Sing = [[x == -1, y == -1],\n",
    "        [x == -1/6*sqrt(7) - 1/6, y == -sqrt(7) + 1],\n",
    "        [x == 1/6*sqrt(7) - 1/6, y == sqrt(7) + 1]\n",
    "       ]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 15,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "(0, 0, 0)\n",
      "(0, 0, 0)\n",
      "(0, 0, 0)\n"
     ]
    }
   ],
   "source": [
    "# Verify that these are singular points and that they are on the curvee:\n",
    "for sing in Sing:\n",
    "    print (X1.subs(sing).expand(),\n",
    "           X2.subs(sing).expand(),\n",
    "           g.subs([x0==1, x1==x, x2==y]).subs(sing).expand()\n",
    "          )"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 16,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Linearization of associated vector field\n",
    "dX = matrix([(diff(X1,x), diff(X1, y)), (diff(X2,x), diff(X2, y))])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "metadata": {},
   "outputs": [],
   "source": [
    "# The matrix M=dX/P at a zero of X gives the matrix I-df\n",
    "M = (dX/P.subs(ex_1p).subs([x0==1, x1==x, x2==y]))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 18,
   "metadata": {},
   "outputs": [],
   "source": [
    "tangent_vector = vector([1, -(y/x)])\n",
    "\n",
    "tangent_ev = (M*tangent_vector)[0]\n",
    "normal_ev = M.trace() - tangent_ev"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "**Tangential eigenvalues:**"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 19,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "<html><script type=\"math/tex; mode=display\">\\newcommand{\\Bold}[1]{\\mathbf{#1}}\\left[-\\frac{3}{2}, -\\frac{1}{3} \\, \\sqrt{7} + \\frac{5}{3}, \\frac{1}{3} \\, \\sqrt{7} + \\frac{5}{3}\\right]</script></html>"
      ],
      "text/plain": [
       "[-3/2, -1/3*sqrt(7) + 5/3, 1/3*sqrt(7) + 5/3]"
      ]
     },
     "execution_count": 19,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "u = [tangent_ev.subs(Sing[i]) for i in range(3)]\n",
    "\n",
    "for i in range(3):\n",
    "    u[i] = u[i].simplify_rational()\n",
    "\n",
    "u"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "**Normal eigenvalues:**"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 20,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "<html><script type=\"math/tex; mode=display\">\\newcommand{\\Bold}[1]{\\mathbf{#1}}\\left[-\\frac{1}{2} \\, \\alpha - \\frac{49}{4}, -\\frac{2}{7} \\, \\alpha - \\frac{2}{9} \\, \\sqrt{7} - \\frac{8}{9}, -\\frac{2}{7} \\, \\alpha + \\frac{2}{9} \\, \\sqrt{7} - \\frac{8}{9}\\right]</script></html>"
      ],
      "text/plain": [
       "[-1/2*alpha - 49/4,\n",
       " -2/7*alpha - 2/9*sqrt(7) - 8/9,\n",
       " -2/7*alpha + 2/9*sqrt(7) - 8/9]"
      ]
     },
     "execution_count": 20,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "v = [normal_ev.subs(Sing[i]) for i in range(3)]\n",
    "\n",
    "for i in range(3):\n",
    "    v[i] = v[i].simplify_rational()\n",
    "\n",
    "v"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "In the case of an invariant line, the following sums are constant: $ \\sum\\frac{1}{u_i}$, $\\sum\\frac{v_i}{u_i}. $"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 21,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "<html><script type=\"math/tex; mode=display\">\\newcommand{\\Bold}[1]{\\mathbf{#1}}1</script></html>"
      ],
      "text/plain": [
       "1"
      ]
     },
     "execution_count": 21,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "L_tan = sum(1/u[k] for k in range(3)).simplify_rational()\n",
    "L_tan  # Tangential Lefschetz formula has to equal 1"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 22,
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "data": {
      "text/html": [
       "<html><script type=\"math/tex; mode=display\">\\newcommand{\\Bold}[1]{\\mathbf{#1}}-\\frac{1}{7} \\, \\alpha + \\frac{37}{6}</script></html>"
      ],
      "text/plain": [
       "-1/7*alpha + 37/6"
      ]
     },
     "execution_count": 22,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "L_rel = sum(v[k]/u[k] for k in range(3)).simplify_rational()\n",
    "L_rel  # 'Relative formula' doesn't give a constant sum"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 8.7",
   "language": "",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 2
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython2",
   "version": "2.7.15"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
